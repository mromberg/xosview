dnl
dnl $Id: configure.in,v 1.4 1996/09/06 00:57:32 bgrayson Exp $
dnl
AC_INIT(xosview.1)
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_HOST
AC_PROG_CXX
AC_PATH_X
AC_PREFIX_DEFAULT(/usr/local)
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_PROG_INSTALL
AC_PROG_AWK

dnl
dnl Add an option which will enable or disable automatic dependencies.
dnl
AC_ARG_ENABLE([auto-depend],
[  --enable-auto-depend    enable automatic dependencies],

if test "$enableval" = "no"
then
        AUTODEPEND=
        echo "disabled automatic dependencies"
else
        AUTODEPEND='-include $(DEPFILES)'
        echo "enabled  automatic dependencies"
fi
,
AUTODEPEND=
echo "disabled automatic dependencies by default"
)
AC_SUBST(AUTODEPEND)

dnl
dnl Call each ports macro so that it can set up its own 
dnl options / configuration.
dnl
case $host_os in
linux)   AC_XOSV_LINUX  ;;
##  Check for known versions of NetBSD...
dnl  Notice that we need to use [] to 'quote' the brackets --
dnl    according to GNU m4 and autoconf, [ and ] are the
dnl    quote characters.
dnl  This check is ugly.  Maybe it should be hidden away in aclocal?
dnl    bgrayson 9/96
netbsd1.[[012]]*)  AC_XOSV_NETBSD ;;
##  Also check for versions that have not been tested.
netbsd*)  AC_MSG_WARN([
***************  Warning  *****************
  xosview has not been tested on $host_os.
  So far, it is known to work on the following NetBSD versions:
    netbsd1.0
    netbsd1.1
    netbsd1.2
  If it works for version $host_os, please send mail to:
	bgrayson@ece.utexas.edu.
  If this message is in error, also notify bgrayson -- we're new
to autoconf!
  Thanks!
*******************************************
]); AC_XOSV_NETBSD ;;

hpux)    AC_XOSV_HPUX   ;;
*)       AC_MSG_ERROR([xosview has not been ported to $host_os :(.  Sorry.]) ;;
esac

dnl
dnl The nethsd and linux ports modify these variables which
dnl are exported by AC_OUTPUT.
dnl
AC_SUBST(EXTRALIBS)
AC_SUBST(INSTALL_ARGS)

AC_OUTPUT(\
  Makefile:config/Makefile.top.in \
  $host_os/Makefile:config/Makefile.$host_os.in \
  Makefile.config:config/Makefile.config.in)

