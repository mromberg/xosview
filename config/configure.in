dnl
dnl $Id: configure.in,v 1.23 1998/05/10 07:08:13 bgrayson Exp $
dnl
AC_INIT(xosview.1)
CF_TOP_SRCDIR
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_HOST
AC_PROG_CXX
AC_PATH_X
dnl AC_PREFIX_PROGRAM(X)
dnl AC_PREFIX_DEFAULT(/usr/X11)	dnl No longer needed with our
dnl 				x_includes magic in Makefile.top.in.
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_RANLIB

dnl
dnl Add an option which will enable or disable automatic dependencies.
dnl
AC_ARG_ENABLE([auto-depend],
[  --enable-auto-depend    enable automatic dependencies],

if test "$enableval" = "no"
then
        AUTODEPEND=
	AUTODEPRULE='\
##  Autodepend rule defaults to disabled.\
#-include Makefile.GNU.autodep'
	AUTODEPRULE_SUBDIR='\
##  Autodepend rule defaults to disabled.\
#-include ../Makefile.GNU.autodep'
        echo "disabled automatic dependencies"
else
        AUTODEPEND='-include $(DEPFILES)'
	AUTODEPRULE='-include Makefile.GNU.autodep'
	AUTODEPRULE_SUBDIR='-include ../Makefile.GNU.autodep'
        echo "enabled  automatic dependencies"
fi
,
AUTODEPEND=
AUTODEPRULE='\
##  Autodepend rule defaults to disabled.\
#-include Makefile.GNU.autodep'
AUTODEPRULE_SUBDIR='\
##  Autodepend rule defaults to disabled.\
#-include ../Makefile.GNU.autodep'
echo "disabled automatic dependencies by default"
)
AC_SUBST(AUTODEPRULE)
AC_SUBST(AUTODEPRULE_SUBDIR)
AC_SUBST(AUTODEPEND)

dnl
dnl Call each ports macro so that it can set up its own 
dnl options / configuration.
dnl
case $host_os in
linux*)   AC_XOSV_LINUX  
          host_os=linux ;;
##  Check for known versions of NetBSD...
dnl  Notice that we need to use [] to 'quote' the brackets --
dnl    according to GNU m4 and autoconf, [ and ] are the
dnl    quote characters.
dnl  This check is ugly.  Maybe it should be hidden away in aclocal?
dnl    bgrayson 9/96
netbsd1.[[0123]]*)  AC_XOSV_NETBSD
  ## Also check for the existence of UVM.  This is a nasty,
  ## complicated check.
  ## UVM is enabled by a line in /etc/mk.conf of the form UVM=.
  ## Note that there may be text after the =, or there might not.
  ## In addition, people may put spaces
  ## in places, so I believe the safest thing is, grep any lines
  ## containing UVM out of /etc/mk.conf, do some substitutions to
  ## remove comments and whitespace, and then check if the
  ## contents are still nonnull.
  #  First, grab the UVM string out of /etc/mk.conf (and silently
  #  ignore if /etc/mk.conf doesn't exist.)
  uvmstring=`grep UVM /etc/mk.conf 2>/dev/null`
  #  Now, replace any whitespace around the =.
  uvmstring=`echo $uvmstring | sed -e 's/ *= */=/'`
  #  Now, remove any trailing whitespace, and strip comments.
  uvmstring=`echo $uvmstring | sed -e 's/[ 	]*$//;s/#.*$//'`
  #  At this point, we should have either the empty string or
  #  UVM=string, where string may be empty.
  if test x$uvmstring != x ; then
    AC_DEFINE(UVM)
    AC_MSG_WARN([

  *Enabling* UVM -- 'UVM=' found in /etc/mk.conf.
  If this is in error, remove the define of UVM=1 from CXXFLAGS
  in Makefile.config, and mail bgrayson@ece.utexas.edu that
  the autoconf script didn't work properly.
    ])
  else
    AC_MSG_WARN([

  *Disabling* UVM -- 'UVM=' not found in /etc/mk.conf.
  If this is in error, add -DUVM=1 to CXXFLAGS in
  Makefile.config, and please mail bgrayson@ece.utexas.edu that
  the autoconf script did not work properly.
  ])
  fi

;;
##  Also check for versions that have not been tested.
netbsd*)  AC_MSG_WARN([
***************  Warning  *****************
  xosview has not been tested on $host_os.
  So far, it is known to work on the following NetBSD versions:
    netbsd1.0
    netbsd1.1
    netbsd1.2
    netbsd1.2.1
    netbsd1.3 through 1.3B
  If it works for version $host_os, please send mail to:
	bgrayson@ece.utexas.edu.
  If this message is in error, also notify bgrayson -- we're new
to autoconf!
  Thanks!
*******************************************
]); AC_XOSV_NETBSD 
    host_os=netbsd ;;

freebsd*) AC_XOSV_FREEBSD
	dnl AC_MSG_WARN([
	dnl ])
	host_os=freebsd ;;
openbsd*) AC_XOSV_OPENBSD
	dnl AC_MSG_WARN([
	dnl ])
	host_os=openbsd ;;
hpux*)    AC_XOSV_HPUX   
          host_os=hpux ;;
*)       AC_MSG_ERROR([xosview has not been ported to $host_os :(.  Sorry.]) ;;
esac

dnl
dnl  Now check all the NetBSD ones for the new swapctl() function.
dnl  I don't know what the status of this is for OpenBSD folks.  Anyone?
dnl
case $host_os in
netbsd)
  AC_CHECK_FUNCS(swapctl) 
  ;;
esac

dnl
dnl The netbsd and linux ports modify these variables which
dnl are exported by AC_OUTPUT.
dnl
AC_SUBST(EXTRALIBS)
AC_SUBST(INSTALL_ARGS)
AC_SUBST(MEMSTAT)
AC_SUBST(LVERSION)
AC_SUBST(USE_MOD_VERSIONS)
dnl  Check for usleep().  Currently, only HP-UX doesn't have it.
AC_CHECK_FUNCS(usleep)

#
# On hpux10 both x_includes and x_libraries seem to be set to ""
# This causes compiler options like '-I' and '-L', which break
# the build.  So, we avoid empty strings here.
#
if test -z "$x_includes"
then
    x_includes=.
fi
if test -z "$x_libraries"
then
   x_libraries=.
fi


AC_OUTPUT(\
  Makefile:config/Makefile.top.in \
  $host_os/Makefile:config/Makefile.$host_os.in \
  Makefile.config:config/Makefile.config.in \
  Makefile.GNU.autodep:config/Makefile.GNU.autodep.in \
  $EXTRA_OUT_FILES)

