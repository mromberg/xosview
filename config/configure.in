#!/bin/bash
AC_INIT([xosview],[1.8.4],
        [https://sourceforge.net/p/xosview/_list/tickets],
        [xosview],
        [http://sourceforge.net/projects/xosview/])
CF_TOP_SRCDIR
AC_CONFIG_HEADERS(config.h:config.h.in)
AC_CONFIG_AUX_DIR(config)

dnl
dnl Have m4 directly include the support macros
dnl rather than deciphering the mysteries of another
dnl auto tool.
dnl
m4_include([m4_ax_config_feature.m4])
m4_include([xo_gcc_auto_depend.m4])
m4_include([xo_bsd_config.m4])

AC_CANONICAL_HOST
AC_PROG_CXX([c++ g++])
ICE_CXX_BOOL
ICE_CXX_LONG_LONG

#
#  x_includes and x_libraries are not longer being exported
# via AC_SUBST.  Because the whole X11 thing is a giant mess.
# For now on configure will just try and figure out what it
# is and insert correct falgs into things like CFLAGS
# x_includes and x_libraries can be used inside the configure script
#
AC_PATH_X
if test -n "$x_includes" ; then
    CXXFLAGS="$CXXFLAGS -I$x_includes"
fi

if test -n "$x_libraries"; then
  LIBS="-L$x_libraries $LIBS"
fi

AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_RANLIB

#------------------------------------------------
# Enable features
#------------------------------------------------

AX_CONFIG_FEATURE_DEFAULT_DISABLED

AX_CONFIG_FEATURE(devel, [turns on debug and auto-depend],
       HAVE_DEVEL, [Does nothing.  autoconf noise],
       [AS_IF([true],[AX_CONFIG_FEATURE_ENABLE(debug)
                      AX_CONFIG_FEATURE_ENABLE(auto-depend)])])

AX_CONFIG_FEATURE_VERBOSE

AX_CONFIG_FEATURE(debug, [turns on/off debug support],
                  XOSVDEBUG, [Define if you want debug support],
                  [xosvdebug="yes"],[xosvdebug="no"])

AX_CONFIG_FEATURE(auto-depend, [turns on/off auto-depend support],
                  AUTO_DEPEND, [Does nothing autoconf noise],
                  [enable_auto_depend="yes"])
XO_GCC_AUTO_DEPEND($enable_auto_depend)

#-------End Features -----------------------------------------

#------------------------------------------------
# Run some tests.  We don't work around failures.
# But if the configure script fails then so will
# the build.  So... early warning.
#------------------------------------------------
CXX_LIBRARY_SUPPORT

#------------------------------------------------
# fnmatch is only used in supressing logDebug
# and there is a workaround if it is not there.
# Not sure how common this function is.
#------------------------------------------------
AC_CHECK_HEADERS([fnmatch.h])
AC_CHECK_FUNCS([fnmatch])

#  Check for usleep().  Currently, only HP-UX doesn't have it.
AC_CHECK_FUNCS(usleep)

AC_LANG_POP([])

#------------------------------------------------
# Used for pixmap backgrounds.
#------------------------------------------------
CPPFLAGS='-I /usr/X11R6/include'
AC_CHECK_HEADER(X11/xpm.h,
AC_CHECK_LIB(Xpm, XpmCreateImageFromData,
   [AC_DEFINE(HAVE_XPM,[1],[Have libXpm])
   XPMLIB=-lXpm],,-lX11))


#
# Call each ports macro so that it can set up its own
# options / configuration.
#
case $host_os in
    linux*)
        AC_XOSV_LINUX
	AC_GCC_EXTRA_CXXFLAGS
	host_dir=linux
        host_os=linux
        ;;
    netbsd*|freebsd*|openbsd*|bsdi*)
        XO_BSD_CONFIG
        ;;
    hpux*)
        AC_XOSV_HPUX
	AC_GCC_EXTRA_CXXFLAGS
	host_dir=hpux
        host_os=hpux
        ;;
    irix6.5*)
        AC_XOSV_IRIX65
	EXTRA_CXXFLAGS=-O2
	host_dir=irix65
	host_os=irix65
        ;;
    solaris2*)
	AC_GCC_EXTRA_CXXFLAGS
	EXTRALIBS="-R$x_libraries -lsocket -lnsl -lkstat $XPMLIB"
	host_dir=sunos5
	host_os=sunos5
        ;;
    gnu*)
        AC_XOSV_GNU
	AC_GCC_EXTRA_CXXFLAGS
	host_dir=gnu
	host_os=gnu
        ;;
    *)
        AC_MSG_ERROR([xosview has not been ported to $host_os :(.  Sorry.])
        ;;
esac

#
# The netbsd and linux ports modify these variables which
# are exported by AC_OUTPUT.
#
AC_SUBST(BTRYMETER)
AC_SUBST(EXTRA_CXXFLAGS)
AC_SUBST(EXTRALIBS)
AC_SUBST(ELF_LINK_FLAGS)
AC_SUBST(INSTALL_ARGS)
AC_SUBST(LVERSION)
AC_SUBST(USE_MOD_VERSIONS)
AC_SUBST(host_dir)
AC_SUBST(NetMeter_Default_Setting)

AC_CONFIG_FILES([
  Makefile:config/Makefile.top.in
  $host_dir/Makefile:config/Makefile.$host_dir.in
  Makefile.config:config/Makefile.config.in
  Makefile.GNU.autodep:config/Makefile.GNU.autodep.in
  Xdefaults:Xdefaults.in
  xosview.1:xosview.1.in
  $EXTRA_OUT_FILES])

AC_OUTPUT
