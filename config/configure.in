#!/bin/bash

AC_INIT([xosview],[2.2.2],
        [https://sourceforge.net/p/xosview/_list/tickets],
        [xosview2],
        [http://sourceforge.net/projects/xosview/])
AC_CONFIG_HEADERS(config.h:config.h.in)
AC_CONFIG_AUX_DIR(config)
AC_CONFIG_MACRO_DIR([config])

#------------------------------------------------
# Support macros
#------------------------------------------------
m4_include([xo_concat.m4])
m4_include([m4_ax_config_feature.m4])
m4_include([xo_min_cxx_check.m4])
m4_include([xo_gcc_auto_depend.m4])
m4_include([xo_linux_config.m4])
m4_include([xo_bsd_config.m4])
m4_include([xo_xft_support.m4])
m4_include([xo_define_dir.m4])
m4_include([xo_smlibs.m4])
m4_pattern_allow(AC_MSG_WARN)
m4_include([m4_ax_cxx_compile_stdcxx.m4])
m4_include([xo_cxx_thread_sleep.m4])

AC_CANONICAL_HOST

# These are the only variables exported
# to and used by the Makefiles.   So,
# Everything configure does must
# add it to one of these or nothing else.
# The exception is CXXFLAGS which is special
# in that it can be overrided at build time.
# Put nothing into CXXFLAGS that is important
# to a proper build.
CPPFLAGS=${CPPFLAGS:-}
CXXFLAGS=${CXXFLAGS:-}
LDFLAGS=${LDFLAGS:-}
LIBS=${LIBS:-}

# Find and set the C++ compiler.
# Everything is C++ no need to change this again.
#AC_PROG_CXX([c++ g++])
AC_PROG_CXX([c++ clang++ c++-5 c++-6 c++-7f g++-5 g++-6 g++-7 g++])
AC_LANG([C++])

#------------------------------------------------
# Enable features (which are compiler related)
#------------------------------------------------

AX_CONFIG_FEATURE_DEFAULT_DISABLED

AX_CONFIG_FEATURE(devel, [turns on debug and auto-depend],
       HAVE_DEVEL, [Does nothing.  autoconf noise],
       [AS_IF([true],[AX_CONFIG_FEATURE_ENABLE(debug)
                      AX_CONFIG_FEATURE_ENABLE(auto-depend)])])

AX_CONFIG_FEATURE_VERBOSE

AX_CONFIG_FEATURE(debug, [turns on/off debug support],
                  XOSVDEBUG, [Define if you want debug support],
                  [xosvdebug="yes"],[xosvdebug="no"])

AX_CONFIG_FEATURE(auto-depend, [turns on/off auto-depend support],
                  AUTO_DEPEND, [Does nothing autoconf noise],
                  [enable_auto_depend="yes"])
XO_GCC_AUTO_DEPEND($enable_auto_depend)

#-------End Features -----------------------------------------

XO_GCC_CXXFLAGS

# Let the user have a stab at app-defaults if they don't
# like the default (${datarootdir}/X11/app-defaults)
xapploaddir="\${datarootdir}/X11/app-defaults"
AC_ARG_WITH([app-defaults],
            [AS_HELP_STRING([--with-app-defaults=DIR],dnl
                [X resource directory [DATAROOTDIR/X11/app-defaults]])],
            [xapploaddir="$with_app_defaults"])
AC_DEFINE_DIR([XAPPLOADDIR],[xapploaddir],dnl
    [location of xosview Xdefaults])


# Programs
AC_PROG_AWK
AC_PROG_RANLIB
AC_PROG_INSTALL


#------------------------------------------------
# Run some tests.  We don't work around failures.
# But if the configure script fails then so will
# the build.  So... early warning.
#------------------------------------------------
# XO_MIN_CXX_CHECK
AX_CXX_COMPILE_STDCXX([14],[noext],[mandatory])

# Can we use a modern c++ sleep?
XO_CXX_THREAD_SLEEP


# =============================================
# AC_PATH_XTRA sets these.  AC_PATH_X does not!
# X_CFLAGS (-I/where/is/X)
# X_LIBS (-L/where/is/X -R/where/is/X)
# X_LIBS is just flags.  Not libraries.
# =============================================
AC_PATH_XTRA

# First add what comes from AC_PATH_XTRA
XO_CONCAT(CPPFLAGS,$CPPFLAGS,"$X_CFLAGS")
XO_CONCAT(LDFLAGS,$LDFLAGS,"$X_LIBS")

# This takes care of the paths.  The libraries themselves (like -lX11)
# will be added later.  First need to check for Xpm, Xft etc.
# Any checks for X11 depended LIBS will need to add
# $X_PRE_LIBS -lX11 $X_EXTRA_LIBS to thier checks.


#---------------------------------------------------
# Session management library support (-lSM -lICE).
#---------------------------------------------------
AC_ARG_WITH([xsm],
            [AS_HELP_STRING([--with-xsm],
              [enable experimental X11R6 session management support])],
            [XO_CHECK_SM_LIBS],
            [with_xsm=no])

#------------------------------------------------
# Used for pixmap backgrounds.
#------------------------------------------------
AC_CHECK_HEADER(X11/xpm.h,
AC_CHECK_LIB(Xpm, XpmCreateImageFromData,
   [AC_DEFINE(HAVE_XPM,[1],[Have libXpm])
   XO_CONCAT(LIBS,$LIBS,[-lXpm])],,-lX11))

#------------------------------------------------
# Double buffering extension
#------------------------------------------------
AC_CHECK_HEADER(X11/extensions/Xdbe.h,
AC_CHECK_LIB(Xext,XdbeQueryExtension,
    [AC_DEFINE(HAVE_DBE,[1],[Have X11 dbe extension])
    XO_CONCAT(LIBS,$LIBS,[-lXext])],,-lX11),[],[#include <X11/Xlib.h>])

XO_XFT_SUPPORT

#----------------------------------------------------
# Call each ports macro so that it can set up its own
# options / configuration.
#----------------------------------------------------

# list of docs to install.  files to place in ${docdir} (See Makefile.top.in)
DOC_FILES="README"

case $host_os in
    linux*)
        XO_CONCAT(DOC_FILES,$DOC_FILES,doc/README.linux)
        AC_XOSV_LINUX

	host_dir=linux
        host_os=linux
        ;;
    netbsd*|freebsd*|openbsd*|bsdi*|dragonfly*)
        XO_CONCAT(DOC_FILES,$DOC_FILES,doc/README.bsd)
        XO_BSD_CONFIG
        ;;
    solaris2*)
        AC_CHECK_HEADER(kstat.h,
            AC_CHECK_LIB(kstat, kstat_open,
            [AC_DEFINE(HAVE_KSTAT,[1],[Have libkstat])
             XO_CONCAT(LIBS,$LIBS,[-lkstat])],,))
	host_dir=sunos5
	host_os=sunos5
        ;;
    gnu*)
        XO_CONCAT(DOC_FILES,$DOC_FILES,doc/README.gnu)
	host_dir=gnu
	host_os=gnu
        ;;
    cygwin)
        AC_CHECK_HEADER(pdh.h)
        AC_CHECK_LIB(pdh, PdhOpenQuery,
            [AC_DEFINE(HAVE_PDH,[1],[Have libpdh])
             XO_CONCAT(LIBS,$LIBS,[-lpdh])])
        AC_CHECK_LIB(iphlpapi, GetAdaptersAddresses,
            [AC_DEFINE(HAVE_IPHLPAPI,[1],[Have libiphlpapi])
            XO_CONCAT(LIBS,$LIBS,[-liphlpapi])])
        XO_CONCAT(DOC_FILES,$DOC_FILES,doc/README.cygwin)
        host_dir=cygwin
        ;;
    minix)
        XO_CONCAT(DOC_FILES,$DOC_FILES,doc/README.minix)
        host_dir=minix
        ;;
    hpux*|irix6.5*)
        AC_MSG_WARN([*************************************************])
        AC_MSG_WARN([* xosview has not been recently ported to $host_os])
        AC_MSG_WARN([* sources for old ports are in attic (but need work).])
        AC_MSG_WARN([*************************************************])
        AC_MSG_WARN([configuring as default platform...])
        host_dir=default
        host_os=default
        ;;
    *)
        AC_MSG_WARN([*************************************************])
        AC_MSG_WARN([* xosview has not been ported to $host_os])
        AC_MSG_WARN([*************************************************])
        AC_MSG_WARN([configuring as default platform...])
        host_dir=default
        host_os=default
        ;;
esac


#---------------------------------------------------------------
# Set the X resource class name based on the PACKAGE_TARNAME
# (first two letters capitalized. This is the X11 standard)
#---------------------------------------------------------------
xclass_first=`echo $PACKAGE_TARNAME | cut -c1-2 | tr [a-z] [A-Z]`
xclass_second=`echo $PACKAGE_TARNAME | cut -c3-`
PACKAGE_CLASSNAME="$xclass_first$xclass_second"
AC_DEFINE_UNQUOTED([PACKAGE_CLASSNAME],["$PACKAGE_CLASSNAME"],dnl
    [X11 resource class name])


#---------------------------------------------------------
# The netbsd and linux ports modify these variables which
# are exported by AC_OUTPUT.
#---------------------------------------------------------
AC_SUBST(PACKAGE_CLASSNAME)
AC_SUBST(xapploaddir)
AC_SUBST(XFT_OBJS)
AC_SUBST(XOSV_FONT)
AC_SUBST(BTRYMETER)
AC_SUBST(INSTALL_ARGS)
AC_SUBST(host_dir)
AC_SUBST(DOC_FILES)

AC_CONFIG_FILES([Makefile:config/Makefile.top.in])
AC_CONFIG_FILES([cmeter/Makefile:config/Makefile.cmeter.in])
AC_CONFIG_FILES([$host_dir/Makefile:config/Makefile.$host_dir.in])
AC_CONFIG_FILES([Makefile.config:config/Makefile.config.in])
AC_CONFIG_FILES([Xdefaults:Xdefaults.in])
AC_CONFIG_FILES([xosview.1:xosview.1.in])

#-----------------------------------------------
# Finalize LIBS
#-----------------------------------------------
dnl XO_CONCAT(LIBS,$LIBS,$X_PRE_LIBS)
XO_CONCAT(LIBS,$LIBS,-lX11)
XO_CONCAT(LIBS,$LIBS,$X_EXTRA_LIBS)

#-----------------------------------------------
#  Display the flags and then create the files
#-----------------------------------------------
if true; then
    echo "DEFS          : $DEFS"
    echo "CPPFLAGS      : $CPPFLAGS"
    echo "CXXFLAGS      : $CXXFLAGS"
    echo "LDFLAGS       : $LDFLAGS"
    echo "LIBS          : $LIBS"
fi

AC_OUTPUT
